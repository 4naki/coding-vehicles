@name AI Core By Naki - Sawtooth
#@inputs [Target]:entity
@inputs Entity:entity
@outputs W A S D Shift Mouse1 Space EyeAngles:angle
@persist RefreshRate Entity:entity Target:entity TargetPos:vector Follow Here SetTarget TargetEntity
@persist WalkDistance StopDistance AimRadius AimRange TooClose CamControllerMode Mouse2Aim HeightLimit CanJump
@trigger none
runOnChat(1)
runOnKeys(owner(),1)
if(first()){
    
    Target=owner()
    
    #Entity=entity()
    
    WalkDistance=800
    
    StopDistance=300
    TooClose=200
    
    AimRadius=15
    AimRange=2500
    
    HeightLimit=100
    
    RefreshRate=100
    
    CamControllerMode=1
    CanJump=1
    
    holoCreate(1)
    holoPos(1,entity():pos())
    holoAlpha(1,0)
}

LS=owner():lastSaid():explode(" ")
LS2=owner():lastSaid():explode("")

if(chatClk(owner())){
    if(LS2[1,string]=="."){
        hideChat(1)
    }
    if(LS[1,string]==".follow"){
        Follow=1
        hint("Alright, i will come with you!",2000)
    }
    if(LS[1,string]==".stay"){
        Follow=0
        Here=0
        hint("I will wait here.",2000)
    }
    if(LS[1,string]==".here"){
        Follow=0
        Here=1
        hint("I am on my way!",2000)
    }
    if(LS[1,string]==".target"){
        if(LS[2,string]){
            if(LS[2,string]=="owner"){
                Target=owner()
                hint("Following you!",2000)
            }
        }else{
            SetTarget=1
        }
    }
}

if(SetTarget){
    if(changed(owner():keyAttack1())&owner():keyAttack1()){
        if(owner():aimEntity():isValid()){
            Target=owner():aimEntity()
            hint("Following "+Target:name()+"!",2000)
        }else{
            holoPos(1,owner():aimPos())
            Target=holoEntity(1)
            hint("Target location set to "+round(owner():aimPos())+"!",2000)
        }
        SetTarget=0
    }
}

interval(RefreshRate)

Distance=round(Target:pos():distance(Entity:pos()))
DistanceIgnoreZ=round((Target:pos()*vec(1,1,0)):distance(Entity:pos()*vec(1,1,0)))
DistanceZ=round(Target:pos():z()-Entity:pos():z())
if(Distance>WalkDistance+StopDistance){
    Shift=1
}else{
    Shift=0
}
if(Follow){
    Move=1
}elseif(Here){
    Move=1
}else{
    Move=0
}
if(Distance>StopDistance){
    W=1*Move
    S=0
}elseif(Distance<TooClose){
    Here=0
    W=0
    S=1*Move
}else{
    Here=0
    W=0
    S=0
}

if(-Entity:toLocal(Target:pos()):y()>(Entity:pos():distance(Target:pos())*0.1)){
    D=1*Move
    A=0
}elseif(-Entity:toLocal(Target:pos()):y()<-(Entity:pos():distance(Target:pos())*0.1)){
    D=0
    A=1*Move
}else{
    D=0
    A=0
}
if(CamControllerMode){
    #Acts like cam controller camAng()
if((Target:pos()-Entity:pos()):toAngle():pitch()<180.1){
    EyeAngles=(Target:pos()-Entity:pos()):toAngle()
}else{
    EyeAngles=ang((Target:pos()-Entity:pos()):toAngle():pitch()-360,(Target:pos()-Entity:pos()):toAngle():yaw(),0)
}
}else{
    #Acts like player eyeAngles()
    EyeAngles=(Target:pos()-Entity:pos()):toAngle()
}
if(Target:pos():distance(Entity:pos()+Entity:forward()*Distance)<AimRadius+(Distance/8)){
    Mouse1=1
}else{
    Mouse1=0
}

if(DistanceIgnoreZ<WalkDistance/3){
    if(DistanceZ*CanJump>HeightLimit){
        timer("unjump",1000)
        if(clk("unjump")){
            Space=0
        }else{
            Space=1*CanJump
        }
    }else{
        Space=0
    }
}else{
    Space=0
}

if(dupefinished()){
    reset()
}
